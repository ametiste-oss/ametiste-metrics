buildscript {
    repositories { jcenter() }

    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:2.2.+'
    }
}

apply plugin: 'nebula-aggregate-javadocs'


subprojects {

    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    apply from: "https://raw.githubusercontent.com/ametiste-oss/ametiste-bintray-gradle/master/bintray-ext-ame-oss.gradle"
    apply from: "https://raw.githubusercontent.com/ametiste-oss/ametiste-bintray-gradle/master/bintray-ext.gradle"

    apply plugin: 'maven-publish'

    group 'org.ametiste.metrics'

    version '0.2.0-4-SNAPSHOT'

    sourceCompatibility=1.8
    targetCompatibility=1.8


    ext {
        springVersion = '4.2.0.RELEASE'
        junitVersion = '4.12'
        mockitoVersion = '1.9.5'
        sl4jVersion = '1.7.2'

        bintray_vcsUrl = 'https://github.com/ametiste-oss/ametiste-metrics.git'
    }


    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${sl4jVersion}"
        testCompile(
                "junit:junit:${junitVersion}",
                "org.mockito:mockito-all:${mockitoVersion}",
                'org.codehaus.groovy:groovy-all:2.4.4',
                'org.spockframework:spock-core:1.0-groovy-2.4'
        )
    }

    configurations {
        deployerJars
    }

    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    javadoc {
        options {
            encoding = 'UTF-8'
        }
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'


    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    test.outputs.upToDateWhen {false}
    test.testLogging.showStandardStreams = true

    test {
        afterTest { desc, result ->
            println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

configure(rootProject) {

    description = 'Metrics library'


}
